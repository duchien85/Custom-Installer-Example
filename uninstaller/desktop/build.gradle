apply plugin: "java"

sourceCompatibility = 1.6
sourceSets.main.java.srcDirs = [ "src/" ]

project.ext.mainClassName = "com.ray3k.windowsinstaller.uninstaller.desktop.DesktopLauncher"
project.ext.assetsDir = new File("../core/assets");

task run(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
}

task debug(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
    debug = true
}

task dist(type: Jar) {
    archiveName = "uninstaller.jar"
    from files(sourceSets.main.output.classesDir)
    from files(sourceSets.main.output.resourcesDir)
    from {configurations.compile.collect {zipTree(it)}}
    from files(project.assetsDir);
 
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
}

dist.dependsOn classes

// creates application bundle (executable + runtime)
task javaPackager(type: Exec, dependsOn: dist) {
    workingDir project.projectDir
    commandLine = [
        'javapackager',
        '-deploy',
        '-nosign',
        '-outdir', "${buildDir}/distribution",
        '-srcdir', "${buildDir}/libs",
        '-native', 'image',
        '-name', project.appName,
        '-appclass', project.mainClassName,
        "-Bicon=${project.projectDir}/uninstaller.ico",
        "-BjvmOptions=-splash:uninstaller-splash.png"
    ]
}

// removes bloated runtime created by javapackager
task cleanPackagerRuntime(dependsOn: javaPackager) {
    doLast() {
        File runtimeFile = new File("${buildDir}/distribution/${project.appName}/runtime")
        runtimeFile.deleteDir()
        System.out.println("deleting bloated runtime in " + runtimeFile)
    }
}

// creates a replacement runtime via jlink command (much smaller than jpackager)
task createFinalAppBundle(type: Exec, dependsOn: [cleanPackagerRuntime]) {
    def jdk = System.getProperty("java.home")

    workingDir project.projectDir
    commandLine = [
        'jlink',
        '-p', "${jdk}/jmods",
        '--add-modules', 'java.base,java.desktop,jdk.unsupported',
        '--strip-debug',
        '--no-header-files',
        '--no-man-pages',
        '--strip-native-commands',
        "--vm=server",
        "--compress=2",
        '--output', "${buildDir}/distribution/${project.appName}/runtime"
    ]

    doLast{
        System.out.println("Application '${project.appName}' packaged.")
        System.out.println(" -> location: ${buildDir}/distribution/${project.appName}/")
    }
}

// changes file permission of JavaPackager executable to read and write
task filepermission(type: Exec) {
    commandLine 'attrib', '-R', "${buildDir}/distribution/${project.appName}/${project.appName}.exe"
}

// Use mt.exe to apply a manifest to the JavaPackager executable
task applyUACmanifest(type: Exec, dependsOn: [createFinalAppBundle]) {
    doFirst {
        filepermission.execute();
    }
    
    workingDir project.projectDir
    commandLine = [
        'C:/Program Files (x86)/Windows Kits/10/bin/10.0.17134.0/x64/mt.exe',
        '-manifest', "${project.projectDir}/${project.appName}.exe.manifest",
        "-outputresource:${buildDir}/distribution/${project.appName}/${project.appName}.exe;#1"
    ]
}

task updateDistribution(type: Copy, dependsOn: [dist]) {
    from "${buildDir}/libs"
    into "${buildDir}/distribution/${project.appName}/app"
}

eclipse {
    project {
        name = appName + "-desktop"
        linkedResource name: 'assets', type: '2', location: 'PARENT-1-PROJECT_LOC/core/assets'
    }
}

task afterEclipseImport(description: "Post processing after project generation", group: "IDE") {
  doLast {
    def classpath = new XmlParser().parse(file(".classpath"))
    new Node(classpath, "classpathentry", [ kind: 'src', path: 'assets' ]);
    def writer = new FileWriter(file(".classpath"))
    def printer = new XmlNodePrinter(new PrintWriter(writer))
    printer.setPreserveWhitespace(true)
    printer.print(classpath)
  }
}
